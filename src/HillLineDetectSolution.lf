/**
 * Display the outputs from the line sensors on the
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>.
 * This program spends the first 10 seconds in calibration mode, during which
 * you should move the robot over the light and dark areas that you would like
 * it to detect.  It then switches into measurement mode and displays the
 * measured reflectance of the five IR sensors.
 *
 * @author Abhi Gundrala
 * @author Edward A. Lee
 */
 target C {
    platform: {
      name: "rp2040",
      board: "pololu_3pi_2040_robot"
    },
    threading: false,
  }
  
  import Line from "lib/Line.lf"
  import Display from "lib/Display.lf"
  
  main reactor(calibration_time: time = 10 s, sample_period: time = 100 ms) {
    line = new Line()
    disp = new Display()
  
    timer t(0, sample_period)
    // Timer used to display seconds.
    timer seconds(0, 100 ms)
    timer end_calibration(calibration_time)
    state count: int = 0
  
    reaction(startup) -> disp.line0, disp.line1, disp.line2 {=
      lf_set(disp.line0, "Well");
      lf_set(disp.line1, "Well");
      lf_set(disp.line2, "Well");
    =}
  
    reaction(seconds) -> disp.line3 {=
      static char buf[17];
      snprintf(buf, 17, "time:%8d s", self->count++);
      lf_set(disp.line3, buf);
    =}
  
    reaction(end_calibration) -> line.calibrate {=
      lf_set(line.calibrate, false);
    =}
  
    reaction(t) -> line.trigger {=
      lf_set(line.trigger, true);
    =}
  
    reaction(line.reflect) -> disp.line0, disp.line1, disp.line2 {=
      int judge = 500;
      lf_set(disp.line0, "Well");
      lf_set(disp.line1, "Well");
      lf_set(disp.line2, "Well");
      if(line.reflect->value[0] > judge || line.reflect->value[1] > judge) {
        lf_set(disp.line0,"Left");
      }
      if(line.reflect->value[2] > judge || line.reflect->value[1] > judge || line.reflect ->value[3] > judge) {
        lf_set(disp.line1,"Center");
      }
      if(line.reflect->value[3] > judge || line.reflect->value[4] > judge) {
        lf_set(disp.line2,"Right");
      }
    =}
  }
  